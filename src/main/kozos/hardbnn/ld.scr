OUTPUT_FORMAT("elf32-littleriscv")
OUTPUT_ARCH(riscv)
ENTRY("_start")

MEMORY
{
	rami(rwx)	: o = 0x80080000, l = 0x00004000 /* 16KB */
	ramd(rwx)	: o = 0x80100000, l = 0x0000c000 /* 48KB */
	stack(rw)	: o = 0x8010bff0, l = 0x00000000 /* end of RAM */
	ramp(rw)	: o = 0x8010c000, l = 0x00014000 /* 80KB */
	bnnam(rw)	: o = 0x80180000, l = 0x00004000 /* 16KB */
}

SECTIONS
{
	.text : {
		_text_start = . ;
		*(.text)
		_etext = . ;
	} > rami

	.rodata : {
		_rodata_start = . ;
		*(.strings)
		*(.rodata)
		*(.rodata.*)
		_erodata = . ;
	} > ramd

	.data : {
		_data_start = . ;
		*(.data)
		_edata = . ;
	} > ramd

	.bss : {
		_bss_start = . ;
		*(.bss)
		*(COMMON)
		_ebss = . ;
	} > ramd

	. = ALIGN(4);
	_end = . ;

	.stack : {
		_stack = .;
	} > stack

	.param : {
		_param_start = . ;
		*(.param)
		_eparam = . ;
	} > ramp

	.bnna : {
		_bnna_start = . ;
		*(.bnna)
		_ebnna = . ;
	} > bnnam

}
